plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.andreyyurko.firstapp"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"
        buildConfigField "Boolean", "USE_MOCK_BACKEND_API", 'false'
        // TODO: write server address
        buildConfigField "String", "BACKEND_API_URL", '""'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }

    flavorDimensions "deployment-environment"
    productFlavors {
        mock {
            dimension "deployment-environment"
            applicationIdSuffix ".mock"
            versionNameSuffix ".mock"
            buildConfigField "Boolean", "USE_MOCK_BACKEND_API", 'true'
        }
        dev {
            dimension "deployment-environment"
            applicationIdSuffix ".dev"
            versionNameSuffix ".dev"
            buildConfigField "String", "BACKEND_API_URL", '""'
        }
        stage {
            dimension "deployment-environment"
            applicationIdSuffix ".stage"
            versionNameSuffix ".stage"
            buildConfigField "String", "BACKEND_API_URL", '""'
        }
        prod {
            dimension "deployment-environment"
            versionNameSuffix ".prod"
            // Despite the default config, all the variables are specified explicitly here,
            // because this product flavor is the most sensitive to errors.
            buildConfigField "Boolean", "USE_MOCK_BACKEND_API", 'false'
            buildConfigField "String", "BACKEND_API_URL", '""'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "yurko-${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

kapt {
    correctErrorTypes true
}

hilt {
    enableAggregatingTask = true
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    // HTTP client.
    def retrofitVersion = "2.9.0"
    def okhttpVersion = '5.0.0-alpha.3'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:okhttp-tls:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.3"
    // JSON parser.
    def moshiVersion = '1.12.0'
    implementation "com.squareup.moshi:moshi:$moshiVersion"
    implementation "com.squareup.moshi:moshi-adapters:$moshiVersion"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshiVersion"
    // AndroidX.
    def lifecycleVersion = "2.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation 'androidx.activity:activity-ktx:1.4.0'
    // Image loading.
    def glideVersion = '4.12.0'
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    // View binding helper.
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.5.3'
    // Video player
    def exoPlayerVersion = "2.16.1"
    implementation "com.google.android.exoplayer:exoplayer-core:$exoPlayerVersion"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoPlayerVersion"
    // RecyclerView adapter delegates.
    // https://github.com/sockeqwe/AdapterDelegates
    def adapterDelegatesVersion = '4.3.1'
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl:$adapterDelegatesVersion"
    implementation "com.hannesdorfmann:adapterdelegates4-kotlin-dsl-viewbinding:$adapterDelegatesVersion"
    // Timber logger
    implementation 'com.jakewharton.timber:timber:5.0.1'
    // Navigation.
    def navigationVersion = '2.4.0-beta02'
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    // System insets handling.
    // https://chrisbanes.github.io/insetter
    // https://github.com/chrisbanes/insetter
    implementation 'dev.chrisbanes.insetter:insetter:0.6.1'
    // https://github.com/haroldadmin/NetworkResponseAdapter
    implementation "com.github.haroldadmin:NetworkResponseAdapter:4.2.2"
    // Dependency injection
    def hiltVersion = '2.40.5'
    implementation "com.google.dagger:hilt-android:$hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$hiltVersion"
    implementation "androidx.hilt:hilt-navigation-fragment:1.0.0"

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'com.google.code.gson:gson:2.8.9'
}